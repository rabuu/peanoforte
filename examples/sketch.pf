define addition-neutrum<a> (add a 0) = a
define addition<a b> (add a (succ b)) = (succ (add a b))

example (add 1 1) = 2 {
	(add 1 1)
	by addition
	(succ [add 1 0])
	by addition-neutrum
	(succ 1)
}

theorem add-one<a> (add a 1) = (succ a) {
	(add a 1)
	by addition
	(succ [add a 0])
	by addition-neutrum
	(succ a)
}

example (add 1 1) = 2 {
	(add 1 1)
	by add-one
	(succ 1)
}

theorem add-two<a> (add a 2) = (succ (succ a)) {
	(add a 2)
	by addition
	(succ [add a 1])
	by add-one
	(succ (succ a))
}

theorem addition-neutrum-left<a> (add 0 a) = a
induction a {
	base { (add 0 0) by addition-neutrum 0 }
	step {
		(add 0 (succ a))
		by addition
		(succ [add 0 a])
		by induction
		(succ a)
	}
}

theorem addition-left<a b> (add (succ a) b) = (succ (add a b))
induction b {
	base {
		(add (succ a) 0)
		by addition-neutrum
		(succ [a])
		by rev addition-neutrum
		(succ (add a 0))
	}
	step {
		(add (succ a) (succ b))
		by addition
		(succ [add (succ a) b])
		by induction
		(succ [succ (add a b)])
		by rev addition
		(succ (add a (succ b)))
	}
}

theorem addition-commutativity<a b> (add a b) = (add b a)
induction b {
	base {
		(add a 0)
		by addition-neutrum
		a
		by rev addition-neutrum-left
		(add 0 a)
	}
	step {
		(add a (succ b))
		by addition
		(succ [add a b])
		by induction
		(succ (add b a))
		by rev addition-left
		(add (succ b) a)
	}
}

;; Use `todo` to force (possibly wrong) proofs
example (add 1 1) = 3 { todo }

example (add 1 1) = 3 {
	by addition
	(succ (add 1 0))
	todo
	(succ 2)
}
