axiom addition-neutrum a: (+ a 0) = a
axiom addition a b: (+ a (succ b)) = (succ (+ a b))

example (+ 1 1) = 2 {
	(+ 1 1)
	@addition
	(succ [+ 1 0])
	@addition-neutrum
	(succ 1)
}

theorem add-one a: (+ a 1) = (succ a) {
	(+ a 1)
	@addition
	(succ [+ a 0])
	@addition-neutrum
	(succ a)
}

example (+ 1 1) = 2 {
	(+ 1 1)
	@add-one
	(succ 1)
}

theorem add-two a: (+ a 2) = (succ (succ a)) {
	(+ a 2)
	@addition
	(succ [+ a 1])
	@add-one
	(succ (succ a))
}

theorem addition-neutrum-left a: (+ 0 a) = a
induction a {
	base { (+ 0 0) @addition-neutrum 0 }
	step {
		(+ 0 (succ a))
		@addition
		(succ [+ 0 a])
		@induction
		(succ a)
	}
}

theorem addition-left a b: (+ (succ a) b) = (succ (+ a b))
induction b {
	base {
		(+ (succ a) 0)
		@addition-neutrum
		(succ [a])
		$addition-neutrum
		(succ (+ a 0))
	}
	step {
		(+ (succ a) (succ b))
		@addition
		(succ [+ (succ a) b])
		@induction
		(succ [succ (+ a b)])
		$addition
		(succ (+ a (succ b)))
	}
}

theorem addition-commutativity<a, b> (+ a b) = (+ b a)
induction b {
	base {
		(+ a 0)
		@addition-neutrum
		a
		$addition-neutrum-left
		(+ 0 a)
	}
	step {
		(+ a (succ b))
		@addition
		(succ [+ a b])
		@induction
		(succ (+ b a))
		$addition-left
		(+ (succ b) a)
	}
}

;; Use `trustme` to force (possibly wrong) proofs
example (+ 1 1) = 3 {
	todo
}
